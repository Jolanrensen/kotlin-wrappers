group 'org.jetbrains'
version '1.0-SNAPSHOT'

buildscript {
    apply from: 'versions.gradle'

    repositories {
        // mavenLocal()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.node-gradle:gradle-node-plugin:$node_plugin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
    }
}

subprojects {
    repositories {
        // mavenLocal()
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-js-wrappers"
        }
    }

    def projectName = name

    ext.applyKotlinJS = {
        apply plugin: 'kotlin2js'

        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
            compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
        }

        compileKotlin2Js {
            kotlinOptions {
                outputFile = "$projectDir/build/classes/main/${projectName}.js"
                moduleKind = "commonjs"
                sourceMap = true
                sourceMapEmbedSources = "always"
                freeCompilerArgs += ["-Xir-produce-klib-dir", "-Xir-only"]
            }
        }

        compileTestKotlin2Js {
            kotlinOptions {
                freeCompilerArgs += ["-Xir-produce-js"]
            }
        }
    }

    ext.configurePublishing = { baseVersion ->
        apply plugin: 'com.github.node-gradle.node'
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'maven-publish'
        apply plugin: 'java'

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives sourcesJar
        }

        def v = "$baseVersion-kotlin-$kotlin_version-ir-$js_ir_version"
        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')
            publish = true
            pkg {
                repo = 'kotlin-js-wrappers'
                name = projectName
                userOrg = 'kotlin'
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/JetBrains/kotlin-wrappers.git'
                version {
                    name = v
                }
            }
            publications = ['Publication']
        }

        publishing {
            publications {
                Publication(MavenPublication) {
                    from components.java
                    groupId 'org.jetbrains'
                    artifactId projectName
                    version v

                    artifact sourcesJar
                }
            }
        }
    }

    ext.configurePublishingWithNPM = { baseVersion ->
        configurePublishing(baseVersion)

        task processPkg(type: Copy) {
            from '.'
            into 'build/npm'
            include 'package.json'
            expand(
                    css_version: "$css_version",
                    kotlin_version: "$kotlin_version",
                    kotlinext_version: "$kotlinext_version",
                    mocha_version: "$mocha_version",
                    react_version: "$react_version",
                    react_dom_version: "$react_dom_version",
                    react_redux_version: "$react_redux_version",
                    react_router_dom_version: "$react_router_dom_version",
                    redux_version: "$redux_version",
                    styled_version: "$styled_version"
            )
        }

        task prepublish(type: Copy) {
            from '.'
            into 'build/npm'
            exclude 'package.json'
            exclude 'build/npm'
        }

        npm_publish {
            args = ['--access', 'public']
            execOverrides {
                it.workingDir = 'build/npm'
            }
        }

        npm_publish.dependsOn prepublish
        npm_publish.dependsOn processPkg
        prepublish.dependsOn build
    }
}
